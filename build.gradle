subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: 'maven-publish'

    group = 'com.excelian.mache'

    sourceCompatibility = 1.8
    version = '0.7-SNAPSHOT'

    repositories {
        maven { url 'https://repository.jboss.org/nexus/content/groups/public' }
        mavenCentral()
    }

    dependencies {
        compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
        compile 'org.slf4j:slf4j-api:1.7.12'

        runtime 'org.apache.logging.log4j:log4j-api:2.3'
        runtime 'org.apache.logging.log4j:log4j-core:2.3'
        runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.3'
        testCompile 'org.hamcrest:hamcrest-all:1.3'
        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-all:1.10.19'
    }

    checkstyle {
        toolVersion = 6.14
        configFile = project(':').file('config/checkstyle/checkstyle.xml')
        configProperties = [ "suppressionFile" : project(':').file('config/checkstyle/suppressions.xml')]
    }

    test {
        beforeTest { descriptor ->
            logger.lifecycle("Running test: " + descriptor)
        }


        testLogging {
            events  "passed", "skipped", "failed", "standardOut", "standardError"
            exceptionFormat = 'full'
            showStackTraces = true
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
        repositories {
            maven {
                url "../releases/$version"
            }
        }
    }
}